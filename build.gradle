plugins {
	id("java-library")
	id("maven-publish")
	id("org.jetbrains.gradle.plugin.idea-ext") version "1.1.8"
	id("eclipse")
	id("com.gtnewhorizons.retrofuturagradle") version "1.4.0"
}

version = "1.0"
group= "com.xcompwiz.lookingglass"
archivesBaseName = "lookingglass"

compileJava.options.encoding = 'UTF-8'
java.sourceCompatibility = java.targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '8'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(8)
	}
}

minecraft {
	version("1.7.10")

	injectedTags.put("VERSION", project.version)
}

tasks.injectTags.configure {
	outputClassName.set("${project.group}.Tags")
}

tasks.processIdeaSettings.configure {
	dependsOn(tasks.injectTags)
}

tasks.deobfuscateMergedJarToSrg.configure {
	accessTransformerFiles.from("src/main/resources/META-INF/lookingglass_at.cfg")
}

// add AT to meta-inf
jar {
	manifest {
		attributes([
			"Implementation-Title"    : project.name,
			"Implementation-Version"  : project.version,
			"FMLCorePluginContainsFMLMod": "true",
			"FMLCorePlugin": "com.xcompwiz.lookingglass.LookingGlassPlugin",
			"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
			"FMLAT": "lookingglass_at.cfg"
		])
	}
}

processResources {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE

	// this will ensure that this task is redone when the versions change.
	inputs.property "version", {"${project.version}"}
	inputs.property "mcversion", {project.minecraft.version}

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		// replace version and mcversion
		expand 'version':"${-> project.version}", 'mcversion':project.minecraft.version
	}

	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}
